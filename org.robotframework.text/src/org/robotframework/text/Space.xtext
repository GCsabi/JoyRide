grammar org.robotframework.text.Space

hidden(COMMENT)

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate space "http://www.robotframework.org/text/Space"

Model: 
	(tables+=RobotTable)+;

RobotTable:
	SettingsTable | TestCaseTable | KeywordTable | VariablesTable;

SettingsTable:
	SettingsHeader
	(settings+=Setting)+;

SettingsHeader:
	STARS ' Settings ' STARS NL
;

TestCaseTable:
	TestCaseHeader
	(tests+=TestCase)+;

TestCaseHeader:
	STARS ' Test Cases ' STARS NL
;

KeywordTable:
	KeywordTableHeader
	(keywords+=Keyword)+;

KeywordTableHeader:
	STARS ' Keywords ' STARS NL
;

VariablesTable:
	VariablesTableHeader
	(variables+=Variable)+;

VariablesTableHeader:
	STARS ' Variables ' STARS NL
;

Setting:
	(OneValueSetting | ValuesSetting | StepSetting);

TestCase:
	name=MacroName NL
	(settings+=TestCaseSetting)*
	(steps+=IndentedStep)+;

Keyword:
	name=MacroName NL
	(settings+=KeywordSetting)*
	(steps+=IndentedStep)+;

Variable:
	ScalarVariable | ListVariable
;

ScalarVariable:
	name=ScalarName SEPARATOR  value=RobotText NL
;

ListVariable:
	name=ListName SEPARATOR value=Values
;

TestCaseSetting:
	SetupSetting | TeardownSetting | SomeTestSetting;

SomeTestSetting:
	SEPARATOR name=TestCaseSettingName SEPARATOR value=Values;

KeywordSetting:
    ArgumentsSetting | TeardownSetting | SomeKeywordSetting;

ArgumentsSetting:
	SEPARATOR name='[Arguments]'
	(SEPARATOR args+=ScalarName)*
	(SEPARATOR default_args+=ScalarName EQ defaults+=RobotText)*
	(SEPARATOR ListName)?
	NL;

TeardownSetting:
    SEPARATOR name='[Teardown]' step=IndentedStep;

SetupSetting:
    SEPARATOR name='[Setup]' step=IndentedStep;


SomeKeywordSetting:
	SEPARATOR name=KeywordSettingName SEPARATOR value=Values;

IndentedStep:
	SEPARATOR RobotStep;

enum TestCaseSettingName:
	DOCUMENTATION='[Documentation]' |
	TIMEOUT='[Timeout]' |
	TAGS='[Tags]' |
	TEMPLATE='[Template]'
;

enum KeywordSettingName:
	DOCUMENTATION='[Documentation]' |
	TIMEOUT='[Timeout]' |
	RETURN='[Return]'
;

OneValueSetting:
	name=RobotOneValueSetting SEPARATOR value=RobotText NL;

ValuesSetting:
	name=RobotValuesSetting SEPARATOR value=Values;

StepSetting:
	name=RobotStepSetting SEPARATOR value=RobotStep;

enum RobotOneValueSetting:
	DOCUMENTATION='Documentation' |
	TEST_TIMEOUT='Test Timeout'
;

enum RobotValuesSetting:
	FORCE_TAGS='Force Tags' |
	DEFAULT_TAGS='Default Tags' |
	LIBRARY_IMPORT='Library' |
	RESOURCE_IMPORT='Resource'
;

enum RobotStepSetting:
	SUITE_SETUP='Suite Setup' |
	SUITE_TEARDOWN='Suite Teardown' |
	TEST_SETUP='Test Setup' |
	TEST_TEARDOWN='Test Teardown'
;

RobotStep:
	keyword=[Keyword|MacroName] (SEPARATOR values+=CellValue)* NL;

CellValue: {CellValue}
	f=RobotText | link=[ScalarVariable | ScalarName]
;

Values:
	val=RobotText (SEPARATOR others+=RobotText)* NL;

ScalarName:
	DOLLAR LBRACKET RobotText RBRACKET
;

ListName:
	ATSIGN LBRACKET RobotText RBRACKET
;

RobotText: (STARS | TEXT) ((' ' | STARS) TEXT)*;

MacroName: TEXT ((' ' | '*') TEXT)*;

terminal SEPARATOR: ' '' '+;

terminal NL: ('\r'|'\n')+;

terminal TEXT: !('='|' '|'\r'|'\n'|'*'|'{'|'}'|'$'|'@')+;

terminal STARS: '*'+;

terminal RBRACKET: '}';
terminal LBRACKET: '{';
terminal DOLLAR: '$';
terminal ATSIGN: '@';
terminal EQ: '=';

terminal COMMENT: ' '* '#' !('\r'|'\n')*;