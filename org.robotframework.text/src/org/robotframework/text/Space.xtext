grammar org.robotframework.text.Space

hidden(COMMENT)

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate space "http://www.robotframework.org/text/Space"

Model: 
	(tables+=RobotTable)+;

RobotTable:
	SettingsTable | TestCaseTable | KeywordTable | VariablesTable;

SettingsTable:
	SettingsHeader
	(settings+=Setting)+;

SettingsHeader:
	STARS ' Settings ' STARS NL
;

TestCaseTable:
	TestCaseHeader
	(tests+=TestCase)+;

TestCaseHeader:
	STARS ' Test Cases ' STARS NL
;

KeywordTable:
	KeywordTableHeader
	(keywords+=Keyword)+;

KeywordTableHeader:
	STARS ' Keywords ' STARS NL
;

VariablesTable:
	VariablesTableHeader
	(variables+=Variable)+;

VariablesTableHeader:
	STARS ' Variables ' STARS NL
;

Setting:
	(OneValueSetting | ValuesSetting | StepSetting);

TestCase:
	name=MacroName NL
	(settings+=TestCaseSetting)*
	(steps+=IndentedStep)+;

Keyword:
	name=MacroName NL
	(settings+=KeywordSetting)*
	(steps+=IndentedStep)+;

Variable:
	ScalarVariable | ListVariable
;

ScalarVariable:
	name=ScalarName SEPARATOR  value=RobotText NL
;

ListVariable:
	name=ListName SEPARATOR value=Values
;

TestCaseSetting:
	SEPARATOR (SetupSetting | TeardownSetting | SomeTestSetting);

SomeTestSetting:
	name=('[Documentation]' |
	'[Timeout]' |
	'[Tags]' |
	'[Template]') 
	SEPARATOR value=Values;

KeywordSetting:
    SEPARATOR setting=(ArgumentsSetting | TeardownSetting | SomeKeywordSetting);

ArgumentsSetting:
	name='[Arguments]'
	(SEPARATOR args+=ScalarName)*
	(SEPARATOR default_args+=ScalarName EQ defaults+=RobotText)*
	(SEPARATOR ListName)?
	NL;

TeardownSetting:
    name='[Teardown]' SEPARATOR step=ExecutingStep
;

SetupSetting:
    name='[Setup]' SEPARATOR step=ExecutingStep;

SomeKeywordSetting:
	name=('[Documentation]' |
	'[Timeout]' |
	'[Return]') 
	SEPARATOR value=Values;

IndentedStep:
	SEPARATOR step=RobotStep;

OneValueSetting:
	name=RobotOneValueSetting SEPARATOR value=RobotText NL;

ValuesSetting:
	name=RobotValuesSetting SEPARATOR value=Values;

StepSetting:
	name=RobotStepSetting SEPARATOR value=ExecutingStep;

RobotOneValueSetting:
	'Documentation' |
	'Test Timeout'
;

RobotValuesSetting:
	'Force Tags' |
	'Default Tags' |
	'Library' |
	'Resource' |
	'Test Template'
;

RobotStepSetting:
	'Suite Setup' |
	'Suite Teardown' |
	'Test Setup' |
	'Test Teardown'
;

RobotStep:
    ExecutingStep | AssigningStep	
;

ExecutingStep:
	keyword=[Keyword|MacroName] (SEPARATOR values+=CellValue)* NL;

AssigningStep:
	(vars+=[Variable|ScalarName] SEPARATOR)* 
	(vars+=[Variable|ScalarName] | vars+=[Variable|ListName]) EQ? 
	SEPARATOR keyword=[Keyword|MacroName] 
	(SEPARATOR values+=CellValue)* NL
;

CellValue: {CellValue}
	f=RobotText | link=[ScalarVariable | ScalarName]
;

Values:
	val=RobotText (SEPARATOR others+=RobotText)* NL;

ScalarName:
	DOLLAR LBRACKET RobotText RBRACKET
;

ListName:
	ATSIGN LBRACKET RobotText RBRACKET
;

RobotText: (STARS | TEXT) ((' ' | STARS) TEXT)*;

MacroName: TEXT ((' ' | '*') TEXT)*;

terminal SEPARATOR: ' '' '+;

terminal NL: ('\r'|'\n')+;

terminal TEXT: !('='|' '|'\r'|'\n'|'*'|'{'|'}'|'$'|'@')+;

terminal STARS: '*'+;

terminal RBRACKET: '}';
terminal LBRACKET: '{';
terminal DOLLAR: '$';
terminal ATSIGN: '@';
terminal EQ: '=';

terminal COMMENT: ' '* '#' !('\r'|'\n')*;