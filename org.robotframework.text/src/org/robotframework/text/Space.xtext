grammar org.robotframework.text.Space

hidden(COMMENT)

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate space "http://www.robotframework.org/text/Space"

Model: 
	(tables+=RobotTable)+;

RobotTable:
	SettingsTable | TestCaseTable | KeywordTable | VariablesTable;

SettingsTable:
	'*** Settings ***' NL
	(settings+=Setting)+;

TestCaseTable:
	'*** Test Cases ***' NL
	(tests+=TestCase)+;

KeywordTable:
	'*** Keywords ***' NL
	(keywords+=Keyword)+;

VariablesTable:
	'*** Variables ***' NL
	(variables+=Variable)+;

Setting:
	(OneValueSetting | ValuesSetting | StepSetting);

TestCase:
	name=MacroName NL
	(settings+=TestCaseSetting)*
	(steps+=IndentedStep)+;

Keyword:
	name=MacroName NL
	(settings+=KeywordSetting)*
	(steps+=IndentedStep)+;

Variable:
	ScalarVariable | ListVariable
;

ScalarVariable:
	'${' name=RobotText '}' SEPARATOR  RobotText NL
;

ListVariable:
	'@{' name=RobotText '}' SEPARATOR Values
;

TestCaseSetting:
	SEPARATOR name=TestCaseSettingName SEPARATOR value=Values;

KeywordSetting:
	SEPARATOR name=KeywordSettingName SEPARATOR value=Values;

IndentedStep:
	SEPARATOR RobotStep;

enum TestCaseSettingName:
	DOCUMENTATION='[Documentation]' |
	TIMEOUT='[Timeout]' |
	SETUP='[Setup]' |
	TEARDOWN='[Teardown]' |
	TAGS='[Tags]' |
	TEMPLATE='[Template]'
;

enum KeywordSettingName:
	DOCUMENTATION='[Documentation]' |
	TIMEOUT='[Timeout]' |
	TEARDOWN='[Teardown]' |
	ARGUMENTS='[Arguments]'|
	RETURN='[Return]'
;

OneValueSetting:
	name=RobotOneValueSetting SEPARATOR value=RobotText NL;

ValuesSetting:
	name=RobotValuesSetting SEPARATOR value=Values;

StepSetting:
	name=RobotStepSetting SEPARATOR value=RobotStep;

enum RobotOneValueSetting:
	DOCUMENTATION='Documentation' |
	TEST_TIMEOUT='Test Timeout'
;

enum RobotValuesSetting:
	FORCE_TAGS='Force Tags' |
	DEFAULT_TAGS='Default Tags' |
	LIBRARY_IMPORT='Library' |
	RESOURCE_IMPORT='Resource'
;

enum RobotStepSetting:
	SUITE_SETUP='Suite Setup' |
	SUITE_TEARDOWN='Suite Teardown' |
	TEST_SETUP='Test Setup' |
	TEST_TEARDOWN='Test Teardown'
;

RobotStep:
	keyword=[Keyword|MacroName] (SEPARATOR RobotText)* NL;

Values:
	RobotText (SEPARATOR RobotText)* NL;

RobotText: ('*' | TEXT) ((' ' | '*') TEXT)*;

MacroName: TEXT ((' ' | '*') TEXT)*;

terminal SEPARATOR: ' '' '+;

terminal NL: ('\r'|'\n')+;

terminal TEXT: !(' '|'\r'|'\n'|'*')+;

terminal COMMENT: ' '* '#' !('\r'|'\n')*;